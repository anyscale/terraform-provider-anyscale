# ---------------------------------------------------------------------------------------------------------------------
# ENVIRONMENT VARIABLES
# Define these secrets as environment variables
# ---------------------------------------------------------------------------------------------------------------------

# AWS_ACCESS_KEY_ID
# AWS_SECRET_ACCESS_KEY

# ---------------------------------------------------------------------------------------------------------------------
# REQUIRED VARIABLES
# These variables must be set when using this module.
# ---------------------------------------------------------------------------------------------------------------------

# ---------------------------------------------------------------------------------------------------------------------
# OPTIONAL VARIABLES
# These variables have defaults but must be included when using this module.
# ---------------------------------------------------------------------------------------------------------------------

variable "aws_region" {
  description = <<-EOT
    (Optional) The AWS region in which all resources will be created.

    ex:
    ```
    aws_region = "us-east-2"
    ```
  EOT
  type        = string
  default     = "us-east-2"
}

variable "tags" {
  description = <<-EOT
    (Optional) A map of tags to all resources that accept tags.

    ex:
    ```
    tags = {
      Environment = "dev"
      Repo        = "terraform-kubernetes-anyscale-foundation-modules",
    }
    ```
  EOT
  type        = map(string)
  default = {
    Test        = "true"
    Environment = "dev"
    Repo        = "terraform-kubernetes-anyscale-foundation-modules",
    Example     = "aws/eks-public"
  }
}

variable "eks_cluster_name" {
  description = <<-EOT
    (Optional) The name of the EKS cluster.

    This will be used for naming resources created by this module including the EKS cluster and the S3 bucket.

    ex:
    ```
    eks_cluster_name = "anyscale-eks-public"
    ```
  EOT
  type        = string
  default     = "anyscale-eks-public"
}

variable "eks_cluster_version" {
  description = <<-EOT
    (Optional) The Kubernetes version of the EKS cluster.

    ex:
    ```
    eks_cluster_version = "1.32"
    ```
  EOT
  type        = string
  default     = "1.36"
}

variable "gpu_instance_types" {
  description = <<-EOT
    (Optional) GPU types configuration for the EKS cluster.
    See gpu_instances.tfvars.example for additional GPU types.

    ex:
    ```
    gpu_instance_types = {
      "T4" = {
        product_name   = "Tesla-T4"
        instance_types = ["g4dn.xlarge", "g4dn.2xlarge", "g4dn.4xlarge"]
      }
      "A10G" = {
        product_name   = "NVIDIA-A10G"
        instance_types = ["g5.4xlarge"]
      }
    }
    ```
  EOT
  type = map(object({
    product_name   = string
    instance_types = list(string)
  }))
  default = {
    "T4" = {
      product_name   = "Tesla-T4"
      instance_types = ["g4dn.4xlarge"]
    }
  }
}

variable "node_group_disk_size" {
  description = <<-EOT
    (Optional) The disk size (GB) of the EKS nodes.
    Possible values: [500, 1000]

    ex:
    ```
    node_group_disk_size = 1000
    ```
  EOT
  type        = number
  default     = 500
}

variable "enable_efs" {
  description = <<-EOT
    (Optional) Enable the creation of an EFS instance.

    This is optional for Anyscale deployments. EFS is used for shared storage between nodes.

    ex:
    ```
    enable_efs = true
    ```
  EOT
  type        = bool
  default     = false
}

variable "cloud_name" {
  description = <<-EOT
    (Optional) The name of the Anyscale cloud.

    ex:
    ```
    cloud_name = "my-eks-cloud"
    ```
  EOT
  type        = string
  default     = "tf-aws-eks-test"
}

variable "anyscale_s3_force_destroy" {
  description = "Force destroy S3 bucket for testing"
  type        = bool
  default     = true
}
